dnl Process this file with autoconf to produce a configure script.
AC_INIT(cpqarrayd.c)

dnl Init automake feature
AM_INIT_AUTOMAKE(cpqarrayd, 1.1)
AC_PROG_MAKE_SET

dnl Checking for OS
AC_MSG_CHECKING(OS name)
OSname=$(uname -s)
AC_MSG_RESULT($OSname)

dnl Checks for programs.
AC_PROG_CC

dnl Checks for libraries.
AC_ARG_WITH(ucd-snmp,
[  --with-ucd-snmp[=PATH]    Specify the ucd-snmp install prefix [/usr] ],
AC_MSG_CHECKING(for ucd-snmp install prefix)
CFLAGS="$CFLAGS -I$withval/include"
LIBS="$LIBS -L$withval/lib"
AC_MSG_RESULT($withval)
,)

dnl Some SNMP packages require libcrypto
AC_CHECK_LIB(crypto, HMAC,,)
AC_CHECK_LIB(snmp, snmp_open,,AC_MSG_ERROR(libsnmp not found))

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/ioctl.h sys/time.h syslog.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Linux specific piece
if test $OSname = Linux ; then

  dnl Check for pathed kernel sources with ida_ioctl.h
  AC_MSG_CHECKING(for SmartArray header directories)
  found_kernel_dir=""
  for kernel_dir in /usr/src/linux/drivers/block
  do
    if test -d $kernel_dir ; then
      CFLAGS="$CFLAGS -I$kernel_dir"
      CPPFLAGS="$CPPFLAGS -I$kernel_dir"
      found_kernel_dir="$kernel_dir"
    fi
  done

  if test "$found_kernel_dir" != "" ; then
    AC_MSG_RESULT(found $found_kernel_dir )
  else
    AC_MSG_RESULT(not found)
  fi

  dnl Check Headers
  AC_CHECK_HEADERS(ida_ioctl.h ida_cmd.h cpqarray.h,, AC_MSG_ERROR(You need to have the SmartArray patch installed.))
  
  dnl Check version of SmartArray driver
  AC_MSG_CHECKING(for SmartArray driver version)
  AC_EGREP_HEADER(blk_cnt, ida_ioctl.h,AC_MSG_RESULT(SmartArray patch version 1.0.1+ installed..), AC_MSG_ERROR(You need to have the SmartArray patch version 1.0.1+ installed.))
fi

dnl FreeBSD specific stuff
if test $OSname = FreeBSD ; then
  dnl Check for the kernel sources
  AC_MSG_CHECKING(for SmartArray header directories)
  if test -d /usr/src/sys/dev/ida ; then
    CFLAGS="$CFLAGS -I/usr/src/sys/dev/ida"
    CPPFLAGS="$CPPFLAGS -I/usr/src/sys/dev/ida"
    AC_MSG_RESULT(/usr/src/sys/dev/ida)
    dnl Check Headers
    AC_CHECK_HEADERS(idareg.h idavar.h,, AC_MSG_ERROR(Can't find needed header files.))
  else
     AC_MSG_ERROR(not found) 
  fi
fi



dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_CHECK_FUNCS(gethostname)

AC_OUTPUT([
Makefile
scripts/Makefile
])
